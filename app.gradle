apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    mavenCentral()
    mavenLocal()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation project(':wwwjdic')
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        def filesAuthorityValue = applicationId + ".fileprovider"
        buildConfigField("String",
                "FILES_AUTHORITY",
                "\"${filesAuthorityValue}\"")

        def searchAuthorityValue = applicationId + ".SearchSuggestionProvider"
        manifestPlaceholders = [searchAuthority: searchAuthorityValue, filesAuthority: filesAuthorityValue]
        buildConfigField("String",
                "SEARCH_AUTHORITY",
                "\"${searchAuthorityValue}\"")
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    dexOptions {
        preDexLibraries = false
    }

    signingConfigs {
        release {
            storeFile file("nick.keystore")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "version"
    productFlavors {
        free {
            dimension "version"
            applicationId = "org.nick.wwwjdic"
        }
        donate {
            dimension "version"
            //applicationId = "org.nick.wwwjdic.donate"
            applicationIdSuffix ".donate"
        }
        ci {
            dimension "version"
            //applicationId = "org.nick.wwwjdic"
            applicationIdSuffix ""
        }
    }

    sourceSets {
        //debug.setRoot('build-types/debug')
        //release.setRoot('build-types/release')

        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        donate {
            manifest.srcFile 'src/donate/AndroidManifest.xml'
            java.srcDirs = ['src/donate/java']
            resources.srcDirs = ['src/donate']
            aidl.srcDirs = ['src/donate/aidl']
            renderscript.srcDirs = ['src/donate/rs']
            res.srcDirs = ['src/donate/res']
            assets.srcDirs = ['src/donate/assets']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${variant.name}-${variant.versionName}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    afterEvaluate {
        generateFreeReleaseBuildConfig.enabled = false
        generateFreeDebugBuildConfig.enabled = false
        generateDonateReleaseBuildConfig.enabled = false
        generateDonateDebugBuildConfig.enabled = false
    }
}

if (project.hasProperty('org_nick_storePassword')
        && project.hasProperty('org_nick_keyAlias')
        && project.hasProperty('org_nick_keyPassword')
        && project.hasProperty('org_nick_storeFile')) {
    android.signingConfigs.release.storeFile = file org_nick_storeFile
    android.signingConfigs.release.storePassword = org_nick_storePassword
    android.signingConfigs.release.keyAlias = org_nick_keyAlias
    android.signingConfigs.release.keyPassword = org_nick_keyPassword
}
